BACKUP ~weidu_external/backup/d2-randomkits~
AUTHOR ~Dan_P~
VERSION ~3.5~
AUTO_EVAL_STRINGS

ALWAYS

  CLEAR_INLINED

  ACTION_BASH_FOR ~%MOD_FOLDER%/lib~ ~.*\.tpa~ BEGIN    // function list
    INCLUDE ~%BASH_FOR_FILESPEC%~
  END
  
  ACTION_DEFINE_ARRAY d2noconvert BEGIN setup END    // tra stuff for classic games
  ACTION_DEFINE_ARRAY d2reload BEGIN main END
  LAF HANDLE_CHARSETS
    INT_VAR
      from_utf8        = 1
      infer_charsets   = 1
    STR_VAR
      default_language = ~english~
      tra_path         = EVAL ~%MOD_FOLDER%/tra~
      out_path         = EVAL ~weidu_external/lang/%MOD_FOLDER%~
      noconvert_array  = ~d2noconvert~
      reload_array     = ~d2reload~
  END

  OUTER_TEXT_SPRINT install    ~%MOD_FOLDER%/install~    // folder variables
  OUTER_TEXT_SPRINT files      ~%MOD_FOLDER%/files~
  OUTER_TEXT_SPRINT abilities  ~%MOD_FOLDER%/abilities~
  OUTER_TEXT_SPRINT passives   ~%MOD_FOLDER%/passives~
  OUTER_TEXT_SPRINT icons      ~%MOD_FOLDER%/icons~
  OUTER_TEXT_SPRINT workspace  ~weidu_external/Workspace~

END

LANGUAGE ~English~
         ~english~
         ~d2-randomkits/tra/english/setup.tra~
         ~d2-randomkits/tra/english/main.tra~
	
LANGUAGE ~French~
         ~french~
         ~d2-randomkits/tra/english/setup.tra~
         ~d2-randomkits/tra/english/main.tra~
         ~d2-randomkits/tra/french/setup.tra~
         ~d2-randomkits/tra/french/main.tra~
		 
		 
//
//Install kits

BEGIN @1
DESIGNATED 9999
REQUIRE_PREDICATE !(GAME_IS ~iwd2~) ~Game not supported~
REQUIRE_PREDICATE (RESOURCE_CONTAINS ~kitlist.2da~ ~BERSERKER~) ~Game not supported~
LABEL ~D2_MISC_KITS~
INCLUDE ~%install%/install.tph~


//
//Choose Enrage version

BEGIN @21
DESIGNATED 20
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~d2-randomkits.tp2~ 9999) ~~
REQUIRE_PREDICATE (RESOURCE_CONTAINS ~kit.ids~ ~D2KENSAIZERKER~) ~~
SUBCOMPONENT @20
NO_LOG_RECORD
LABEL ~D2_MISC_KITS_ENRAGE1~
OUTER_SET enrage_mode = 1
INCLUDE ~%install%/enragepatch.tph~

BEGIN @22
SUBCOMPONENT @20
NO_LOG_RECORD
LABEL ~D2_MISC_KITS_ENRAGE2~
OUTER_SET enrage_mode = 2
INCLUDE ~%install%/enragepatch.tph~


//
//Update abilities (HLAs, profs, mod compat)

BEGIN @30
DESIGNATED 30
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~d2-randomkits.tp2~ 9999) ~~
NO_LOG_RECORD
LABEL ~D2_MISC_KITS_UPDATEHLA~
INCLUDE ~%install%/install30.tph~


////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////

/*
*/