//Jesterblade (d2mk3)


//prep
ACTION_IF (GAME_IS ~bgee bg2ee iwdee eet~) BEGIN
  OUTER_SET jesterblade_desc = RESOLVE_STR_REF (@10303)
END ELSE BEGIN
  OUTER_SET jesterblade_desc = RESOLVE_STR_REF (@9910303)
END

DEFINE_PATCH_FUNCTION jestersaves BEGIN
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur=0 step_size=4 level_cap=5 dur_special=1 END
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur=0 step_size=5 level_cap=20 dur_special=1 min_lev_alt=5 END
  LPF ALTER_EFFECT INT_VAR header=1 match_savingthrow=1 savebonus=1 END
  LPF ALTER_EFFECT INT_VAR header=2 match_savingthrow=1 savebonus=0 END
  LPF ALTER_EFFECT INT_VAR header=3 match_savingthrow=1 savebonus=~-1~ END
  LPF ALTER_EFFECT INT_VAR header=4 match_savingthrow=1 savebonus=~-2~ END
END

DEFINE_PATCH_FUNCTION jesterdur BEGIN
  LPF ALTER_EFFECT INT_VAR match_opcode=128 duration=10 END
  LPF ALTER_EFFECT INT_VAR match_opcode=215 duration=10 END
END


//change song
COPY_EXISTING ~SPCL751.SPL~   ~override/%e1%.SPL~
  WRITE_LONG 0x8 ~-1~
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s1%~ END

//projectile
COPY - ~%abilities%/invisible30enemy.PRO~ ~%workspace%/%s1%.PRO~
ADD_PROJECTILE ~%workspace%/%s1%.PRO~


//check for jester song (confusion effect)
OUTER_TEXT_SPRINT jester_song ~~

ACTION_FOR_EACH spell IN ~SPCL751A~ ~SPCL751B~ BEGIN
  ACTION_IF (~%jester_song%~ STR_EQ ~~) BEGIN
    COPY_EXISTING - ~%spell%.spl~  ~~
      SET confuseop = 0
      SET visualop  = 0
      SET basethac0 = 0

      GET_OFFSET_ARRAY spl_headers  SPL_V10_HEADERS
      PHP_EACH spl_headers AS num => header_off BEGIN      // array of headers
        GET_OFFSET_ARRAY2 spl_effects header_off SPL_V10_HEAD_EFFECTS
        PHP_EACH spl_effects AS num => effect_off BEGIN    // array of effects on each header
          READ_SHORT effect_off opcode
          PATCH_IF (opcode = 128) BEGIN SET confuseop = 1 END
          PATCH_IF (opcode = 215) BEGIN SET visualop  = 1 END
          PATCH_IF (opcode = 54)  BEGIN SET basethac0 = 1 END
        END
      END

      PATCH_IF (confuseop>0 AND visualop>0 AND basethac0=0) BEGIN    // skip if effects modded
        TEXT_SPRINT jester_song ~%spell%~
      END
    IF_EXISTS
  END
END


//clone jester song if found
ACTION_IF !(~%jester_song%~ STR_EQ ~~) BEGIN
  COPY_EXISTING ~%jester_song%.spl~  ~override/%s1%.SPL~
END

//or copy from mod folder
ACTION_IF !(FILE_EXISTS ~override/%s1%.SPL~) BEGIN
  ACTION_IF (GAME_IS ~iwdee~) BEGIN 
    COPY ~%abilities%/jestersongiwd.SPL~  ~override/%s1%.SPL~    // IWDEE
  END ELSE BEGIN 
    COPY ~%abilities%/jestersongbg2.SPL~  ~override/%s1%.SPL~    // BGEE games
  END
END


//set correct string (get blocked by immunity)
OUTER_SET confused  = 14782
ACTION_IF (GAME_IS ~bgee~)  BEGIN  OUTER_SET confused  = 25807 END
ACTION_IF (GAME_IS ~iwdee~) BEGIN  OUTER_SET confused  = 37604 END


//edit song effects
COPY_EXISTING ~%s1%.SPL~  ~override~
  WRITE_LONG 0x8 ~-1~
  LPF d2_remove_level_headers END
  LPF DELETE_EFFECT INT_VAR match_opcode=321 END
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=139 parameter1=confused END    // "Confused"

  PATCH_IF (GAME_IS ~bgee bg2ee iwdee eet~) BEGIN
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=318 STR_VAR resource=~%s1%~ END    // BG games
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=324 STR_VAR resource=~%s1%~ END    // IWDEE
  END ELSE BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=136 END
    LPF DELETE_EFFECT INT_VAR match_opcode=328 END
    LPF DELETE_EFFECT INT_VAR match_opcode=318 END
  END

  LPF jestersaves END
  LPF jesterdur END
BUT_ONLY


//Make clab table
COPY ~inlined/d2clab_blade_nosongs~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    mixed = RESOLVE_STR_REF (@10301)
    lower = RESOLVE_STR_REF (@10302)
    help = jesterblade_desc
  STR_VAR
    kit_name = ~D2%kitname%~
    source_kit = ~BLADE~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~    // Usable by all races, if base class is usable
  RET 
    kit_id
END


//change ToB starting armor (elven chain like regular bard)
ACTION_IF (FILE_EXISTS_IN_GAME ~chan12.itm~) BEGIN
  LAF d2_edit_2da_entry STR_VAR 2da=25STWEAP col_name=~D2%kitname%~ row_name=armor entry=chan12 END
END


//
//Gains blade/jester effects from items
OUTER_TEXT_SPRINT kit_group ~blade jester~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


//Set variables for external use
OUTER_TEXT_SPRINT jesterblade    ~%kitid%~
OUTER_TEXT_SPRINT offensivespin  ~SPCL521~
OUTER_TEXT_SPRINT defensivespin  ~SPCL522~


/*
*/