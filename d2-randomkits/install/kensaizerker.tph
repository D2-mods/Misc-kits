//Kensaizerker (d2mk1)


//prep
ACTION_IF (GAME_IS ~bgee bg2ee iwdee eet~) BEGIN
  OUTER_SET enrage_desc = RESOLVE_STR_REF (@200)
  OUTER_SET kensaizerker_desc = RESOLVE_STR_REF (@10103)
END ELSE BEGIN
  OUTER_SET enrage_desc = RESOLVE_STR_REF (@99200)
  OUTER_SET kensaizerker_desc = RESOLVE_STR_REF (@9910103)
END


//Copy passive effects
COPY ~%passives%/hitdamage1.SPL~         ~override/%e1%.SPL~
COPY ~%passives%/speedfactorkensai.SPL~  ~override/%e2%.SPL~
COPY ~%passives%/acbonus2.SPL~           ~override/%e3%.SPL~ 


//get icon
OUTER_TEXT_SPRINT enrage_icon ~SPIN117B~
COPY_EXISTING - ~SPCL321.spl~  ~~
  READ_ASCII 0x76 enrage_icon (8)
IF_EXISTS

//add Enrage
COPY ~%abilities%/applyeffects.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @100
  WRITE_LONG 0x50 enrage_desc
  WRITE_ASCII 0x3a ~%enrage_icon%~ #8
  LPF ALTER_SPELL_HEADER INT_VAR target=5 STR_VAR icon=~%enrage_icon%~ END
  LPF ALTER_EFFECT STR_VAR resource=d2rageb END    // cast Enrage (enrageprep.tph)
//  WRITE_LONG 0x18 THIS | BIT9    // break sanctuary (use IWD rules, keep invis)
  WRITE_SHORT 0x1c 4


//Make clab table
COPY ~inlined/d2clab_fighter~  ~override/%kitid%.2DA~
  LPF d2clab_artisancheck STR_VAR type=melee END
  LPF d2_kit_ability_adder INT_VAR multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=3 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 passive=1 multi=1 increment=4 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    mixed = RESOLVE_STR_REF (@10101)
    lower = RESOLVE_STR_REF (@10102)
    help = kensaizerker_desc
  STR_VAR
    kit_name = ~D2%kitname%~
    source_kit = ~KENSAI~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~        // Usable by all races, if base class is usable
    alignmnt = ~0 0 0 1 1 1 1 1 1~    // no Lawful
  RET 
    kit_id
END


//backup Enrage
COPY ~override/%s1%.SPL~  ~override/%@1%.SPL~


//hla table
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=berserker END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%kitid%~ END
//LAF d2_clone_hla_table STR_VAR kitids=~%ids_name%~ copy=berserker abbrev=~%kitid%~ RET hlatable END


/*old version
//use berserker HLA table (same as base fighter in unmodded game)
COPY_EXISTING ~luabbr.2da~  ~override~
  LPF d2_get_2da_entry STR_VAR row_name=berserker col_name=abbrev RET entry END
  LPF d2_edit_2da_entry STR_VAR row_name=~D2%kitname%~ col_name=abbrev entry=~%entry%~ END
BUT_ONLY
*/


//Gains kensai/berserker effects from items
OUTER_TEXT_SPRINT kit_group ~kensai berserker~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
INCLUDE ~%install%/beamdogitems.tph~


//Set variables for external use
OUTER_TEXT_SPRINT kensaizerker  ~%kitid%~
OUTER_TEXT_SPRINT enrage        ~%s1%~
OUTER_TEXT_SPRINT enrage_mod    ~%@1%~
OUTER_TEXT_SPRINT enrage_base   ~SPCL321~


/*
*/