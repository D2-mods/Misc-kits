//Monastery Swordsman (d2mk2)


//Copy passive effects
COPY ~%passives%/hitdamage1.SPL~  ~override/%e5%.SPL~

/*using monasteryprofs.tph (more compatible method)
COPY ~%passives%/nobowslingdart.SPL~  ~override/%e2%.SPL~
//  LPF DELETE_EFFECT INT_VAR match_parameter1=15 END
//  LPF DELETE_EFFECT INT_VAR match_parameter1=27 END
  LPF ALTER_EFFECT INT_VAR opcode=181 special=RESOLVE_STR_REF (@303) END
*/


//
//prep for elemental abilities
COPY - ~%abilities%/blazingweapon.SPL~  ~inlined/d2elementweapon.SPL~
  LPF ALTER_SPELL_HEADER INT_VAR header=1 new_header_type=1 speed=0 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=3 END
  LPF ALTER_EFFECT INT_VAR match_duration=6 duration=24 END
  LPF ALTER_EFFECT INT_VAR match_opcode=321 opcode=326 STR_VAR resource=~%s1%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=SPUSEANY END

COPY ~%abilities%/removespelleffects.SPL~  ~override/%s1%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s3%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s4%~ END

COPY ~%abilities%/blazingweapon.EFF~  ~override/%s1%a.EFF~    // fire
  WRITE_SHORT 0x22 8
COPY ~%abilities%/blazingweapon.EFF~  ~override/%s3%a.EFF~    // slashing
  WRITE_SHORT 0x22 256
  WRITE_LONG 0x24 4    // delay 1 second (hits at end of swirl)
  WRITE_LONG 0x28 1
COPY ~%abilities%/blazingweapon.EFF~  ~override/%s4%a.EFF~    // magic
  WRITE_SHORT 0x22 64

COPY ~%icons%/elementweapon.BAM~  ~override/%s1%i.BAM~
COPY ~%icons%/monasteryfire.BAM~  ~override/%s1%.BAM~
COPY ~%icons%/monasterywind.BAM~  ~override/%s3%.BAM~
COPY ~%icons%/monasterymoon.BAM~  ~override/%s4%.BAM~


//casting visual
COPY ~%abilities%/elementweapon.VVC~  ~override/%s1%s.VVC~    // fire
  WRITE_ASCII 0x8 ~%s1%s~ #8
COPY ~%abilities%/elementweapon.VVC~  ~override/%s3%s.VVC~    // wind
  WRITE_ASCII 0x8 ~%s3%s~ #8
COPY ~%abilities%/elementweapon.VVC~  ~override/%s4%s.VVC~    // moon
  WRITE_ASCII 0x8 ~%s4%s~ #8

COPY ~%abilities%/elementfire.BAM~  ~override/%s1%s.BAM~
COPY ~%abilities%/elementwind.BAM~  ~override/%s3%s.BAM~
COPY ~%abilities%/elementmoon.BAM~  ~override/%s4%s.BAM~

//hit visuals
COPY ~%abilities%/applyeffects.EFF~  ~override/%s3%b.EFF~    // wind
  WRITE_LONG  0x10 215
  WRITE_ASCII 0x30 ~%s3%b~ #8
COPY ~%abilities%/applyeffects.EFF~  ~override/%s4%b.EFF~    // moon
  WRITE_LONG  0x10 215
  WRITE_ASCII 0x30 ~%s4%b~ #8

COPY ~%abilities%/swirlwind.VVC~  ~override/%s3%b.VVC~
COPY ~%abilities%/swirlmoon.VVC~  ~override/%s4%b.VVC~
  WRITE_ASCII 0x8 ~%s4%b~ #8
COPY ~%abilities%/swirlmoon.BAM~  ~override/%s4%b.BAM~


//
//add elemental abilities
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@101) STR_VAR bam=~%s1%i~ RET icon END
COPY ~inlined/d2elementweapon.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @101
  SAY UNIDENTIFIED_DESC @201
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF ALTER_EFFECT INT_VAR match_opcode=141 parameter2=22 END
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~%s1%s~ END
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@103) STR_VAR bam=~%s1%i~ RET icon END
COPY ~inlined/d2elementweapon.SPL~  ~override/%s3%.SPL~
  SAY NAME1 @103
  SAY UNIDENTIFIED_DESC @203
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s3%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s3%b~ END    // hit visual
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF ALTER_EFFECT INT_VAR match_opcode=141 parameter2=14 END
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~%s3%s~ END
  WRITE_ASCII 0x3a ~%s3%~ #8
  WRITE_ASCII 0x76 ~%s3%~ #8

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@104) STR_VAR bam=~%s1%i~ RET icon END
COPY ~inlined/d2elementweapon.SPL~  ~override/%s4%.SPL~
  SAY NAME1 @104
  SAY UNIDENTIFIED_DESC @204
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s4%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s4%b~ END    // hit visual
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF ALTER_EFFECT INT_VAR match_opcode=141 parameter2=6 END
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~%s4%s~ END
  WRITE_ASCII 0x3a ~%s4%~ #8
  WRITE_ASCII 0x76 ~%s4%~ #8


//
//Add Sever
COPY ~%icons%/severicon.BAM~ ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@102) STR_VAR bam=~%s2%i~ RET icon END

COPY ~%icons%/sever.BAM~      ~override/%s2%.BAM~
COPY ~%abilities%/sever.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @102
  SAY UNIDENTIFIED_DESC @202
  WRITE_ASCII 0x3a ~%s2%~ #8
  WRITE_ASCII 0x76 ~%s2%~ #8
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=248 STR_VAR resource=~%s2%x~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=248 STR_VAR resource=~%s2%a~ END    // damage
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=248 STR_VAR resource=~%s2%b~ END    // vorpal
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF d2_mindamageboost INT_VAR amount=3 duration=12 END

COPY ~%abilities%/applyeffects.EFF~   ~override/%s2%a.EFF~
  WRITE_ASCII 0x30 ~%s2%a~ #8
COPY ~%abilities%/severslashing.SPL~  ~override/%s2%a.SPL~

COPY ~%abilities%/applyeffects.EFF~   ~override/%s2%b.EFF~
  WRITE_ASCII 0x30 ~%s2%b~ #8
  WRITE_LONG 0x40 4
  WRITE_LONG 0x44 ~-2~
COPY ~%abilities%/d2vorpal.SPL~       ~override/%s2%b.SPL~
  LPF DELETE_EFFECT INT_VAR match_opcode=318 END    // no racial immunities
  LPF DELETE_EFFECT INT_VAR match_opcode=139 END    // no display string

COPY ~%abilities%/castspellself.EFF~       ~override/%s2%x.EFF~
  WRITE_ASCII 0x30 ~%s2%x~ #8
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s2%x.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END


//
//Make clab table
COPY ~inlined/d2clab_monk_noquivstun~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=5 STR_VAR resource=~%e5%~ END

//  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
//  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
//  LPF d2_kit_ability_adder INT_VAR level=13 STR_VAR resource=~%s2%~ END
//  LPF d2_kit_ability_adder INT_VAR level=17 STR_VAR resource=~%s2%~ END

  //for subtledoctor APR tweak (fixes this kit's APR)
  LPF d2_kit_ability_adder INT_VAR level=7  passive=1 STR_VAR resource=~D5_NUMAT~ END
  LPF d2_kit_ability_adder INT_VAR level=13 passive=1 STR_VAR resource=~D5_NUMA2~ END
  LPF d2_kit_ability_adder END


//
//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 20
    mixed = RESOLVE_STR_REF (@10201)
    lower = RESOLVE_STR_REF (@10202)
    help = RESOLVE_STR_REF (@10203)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~      // Usable by all races, if base class is usable
    clascolr = ~115 24 25 24 72~    // enchanter colors
//    weapprof = ~0 5 0 0 5 0 0 5 0 5 5 0 0 5 5 5 0 5 0 0 0 0 0 0 0 0 0 0 0 0 2 3~
    clswpbon = ~1 0 2~              // middle number is levels per fist 1/2 apr bonus
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=monk END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%kitid%~ END
//LAF d2_clone_hla_table STR_VAR kitids=~%ids_name%~ copy=monk abbrev=~%kitid%~ RET hlatable END


//Gains kensai effects from items
OUTER_TEXT_SPRINT kit_group ~kensai~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
INCLUDE ~%install%/beamdogitems.tph~


//Set variables for external use
OUTER_TEXT_SPRINT monastery   ~%kitid%~
OUTER_TEXT_SPRINT elementfire  ~%s1%~
OUTER_TEXT_SPRINT elementwind  ~%s3%~
OUTER_TEXT_SPRINT elementmoon  ~%s4%~
OUTER_TEXT_SPRINT sever        ~%s2%~


/*
*/