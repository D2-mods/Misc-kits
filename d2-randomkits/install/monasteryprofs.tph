/*
all profs are only set if base class can learn it
anything set higher than fighter class is automatically lowered to match it
*/


ACTION_CLEAR_ARRAY ~d2_weapontype_usable~
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2_weapontype_usable BEGIN    // type => slots
  axe            => 7
  bastardsword   => 7
  club           => 7
  compositebow   => 0
  dagger         => 7
  dart           => 0
  flail          => 7    // if usable and unusable items share a prof, usable has priority
  greataxe       => 7    // can leave items off array (no prof, but no op319 if restrictitems > 0)
  greatsword     => 7
  halberd        => 7
  hammer         => 7
  heavycrossbow  => 0
  katana         => 7
  lightcrossbow  => 0
  longbow        => 0
  longsword      => 7
  mace           => 7
  morningstar    => 7
  ninjato        => 7
  scimitar       => 7
  shortbow       => 0
  shortsword     => 7
  sling          => 0
  spear          => 7
  staff          => 7
  throwingdagger => 7
  throwingaxe    => 0
  throwinghammer => 0
  wakizashi      => 7
END

OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
OUTER_TEXT_SPRINT baseclass  ~monk~
OUTER_SET stringref = RESOLVE_STR_REF (@10201)    // kit name (used if restrictitems > 0)
OUTER_SET restrictitems = 1
OUTER_SET stylemode = 5
OUTER_SET clearprofs = 1
OUTER_SET force2weapon = 1
INCLUDE ~%install%/usableweapons.tph~



//
//bonus 2 slots in 2 handed (if monks have it)
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
OUTER_TEXT_SPRINT profbonus  ~override/%e6%.SPL~
OUTER_SET 2handed = 111
LAF d2_get_2da_entry INT_VAR col_read=1 STR_VAR 2da=weapprof col_name=~%ids_name%~ row_name=~%2handed%~ RET entry END

COPY ~%profbonus%~  ~%profbonus%~
  LPF DELETE_EFFECT INT_VAR match_parameter2=2handed END
  PATCH_IF (IS_AN_INT entry AND entry > 0) BEGIN
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=233 parameter2=2handed END
  END
IF_EXISTS BUT_ONLY

//check kit desc
LAF GET_KIT_STRREF STR_VAR kit_name=~%ids_name%~ RET kit_strref END
ACTION_GET_STRREF EVAL ~%kit_strref%~ desc
OUTER_SPRINT base  @450
OUTER_SPRINT mod  @451
OUTER_SPRINT check  @452
OUTER_SPRINT dash  @453
OUTER_SPRINT note  @454
OUTER_SPRINT space  ~ ~

//tof fix (if it overwrites base line)
ACTION_IF (~%desc%~ STRING_CONTAINS_REGEXP ~%base%~) BEGIN    // if "desc" does NOT contain "base" text
  OUTER_PATCH_SAVE desc ~%desc%~ BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH ~%check%~  ~%base%%space%%note%%WNL%%dash%%check%~
  END	
  STRING_SET_EVALUATE kit_strref  ~%desc%~
END

//
ACTION_IF (IS_AN_INT entry AND entry > 0) BEGIN
  ACTION_IF (~%desc%~ STRING_CONTAINS_REGEXP ~%mod%~) BEGIN    // if "desc" does NOT contain "mod" text
    OUTER_PATCH_SAVE newdesc ~%desc%~ BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ~%base%~  ~%mod%~    // add "mod" text
    END	
    STRING_SET_EVALUATE kit_strref  ~%newdesc%~
  END
END ELSE BEGIN
  ACTION_IF !(~%desc%~ STRING_CONTAINS_REGEXP ~%mod%~) BEGIN    // if "desc" DOES contain "mod" text
    OUTER_PATCH_SAVE newdesc ~%desc%~ BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ~%mod%~  ~%base%~    // remove "mod" text
    END	
    STRING_SET_EVALUATE kit_strref  ~%newdesc%~
  END
END



//
//mod stuff

//for Combat Skills mod (melee bonus)
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT type ~MELEETHAC0~
LAF d2_edit_2da_entry STR_VAR 2da=weapprof col_name=~%ids_name%~ row_name=~%type%~ entry=4 END

//for Skills & Abilities (no Archery)
OUTER_TEXT_SPRINT type ~ARCHERY~
LAF d2_edit_2da_entry STR_VAR 2da=weapprof col_name=~%ids_name%~ row_name=~%type%~ entry=0 END


//tof - give monastery swordsman correct profs
//if tof is changed to account for prof revisions, this does nothing (skipped silently)
OUTER_TEXT_SPRINT file ~override/m_dw_krd.lua~

OUTER_TEXT_SPRINT line
  ~[43]={[89]=5, [90]=5, [91]=5, [92]=5, [93]=5, [94]=5, [95]=5, [96]=5, [97]=5, [98]=5, [99]=5, [100]=5, [101]=5, [102]=2, [103]=2, [104]=2, [105]=2, [106]=0, [107]=0, [111]=2, [112]=0, [113]=2, [114]=3, [115]=5, },~

OUTER_TEXT_SPRINT newline
  ~[43]={[89]=5, [90]=5, [91]=5, [92]=5, [93]=5, [94]=5, [95]=5, [96]=5, [97]=5, [98]=5, [99]=5, [100]=5, [101]=5, [102]=5, [103]=0, [104]=0, [105]=0, [106]=0, [107]=0, [111]=2, [112]=0, [113]=2, [114]=3, [115]=5, },~

COPY ~%file%~  ~%file%~
  REPLACE_TEXTUALLY EXACT_MATCH  ~%line%~  ~%newline%~
IF_EXISTS BUT_ONLY


//
//