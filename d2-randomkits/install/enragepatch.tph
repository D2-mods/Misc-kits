/*
Choose Enrage version
*/


//
OUTER_TEXT_SPRINT kitid ~d2mk1~
OUTER_TEXT_SPRINT kitname ~KENSAIZERKER~
INCLUDE ~%install%/variables.tph~

OUTER_TEXT_SPRINT enrage ~SPCL321~
OUTER_TEXT_SPRINT winded ~SPCL321D~
OUTER_SET ee_game = 0
ACTION_IF (RESOURCE_CONTAINS ~action.ids~ ~zoomlock~) BEGIN OUTER_SET ee_game = 1 END


//custom Enrage (copy backup file)
ACTION_IF (enrage_mode = 1) BEGIN
  COPY_EXISTING ~%@1%.SPL~  ~override/%s1%.SPL~  IF_EXISTS

  COPY_EXISTING ~%winded%.SPL~  ~override~
    LPF DELETE_EFFECT STR_VAR match_resource=~%s1%~ END    // remove effect from winded (if added by Option 2)
  BUT_ONLY
END


//base game Enrage (copy SPCL321.spl)
ACTION_IF (enrage_mode = 2) BEGIN

  // clone and edit Enrage
  COPY_EXISTING ~%enrage%.SPL~  ~override/%s1%.SPL~

    // update resource fields
    LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%enrage%~ resource=~%s1%~ END

    // EEs: no damage/winded after rest
    PATCH_IF (ee_game > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=12 opcode=321 timing=4 duration=61 parameter2=0 STR_VAR resource=~%s1%~ END
    END

    // IWDEE: does not prevent resting
    LPF DELETE_EFFECT INT_VAR match_opcode=338 END

    // recast message
    SET norecast = RESOLVE_STR_REF (@302)
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=206 parameter1=norecast STR_VAR match_resource=~%s1%~ END


  // add effect to winded
  COPY_EXISTING ~%winded%.SPL~  ~override~
    LPF DELETE_EFFECT STR_VAR match_resource=~%s1%~ END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=206 parameter1=norecast STR_VAR match_resource=~%enrage%~ resource=~%s1%~ END
  BUT_ONLY

END


//
//