/*
mod compat for jesterblade
mods: rr, IWDification, Bardic Wonders, Shohy's bard revision, ToF
*/


//restore default song
COPY ~override/%@1%.SPL~  ~override/%s1%.SPL~ IF_EXISTS


//variables
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
OUTER_TEXT_SPRINT hlatable  ~lu%kitid%~    // clone of blade table


//clear mod stuff from clab
COPY_EXISTING ~%kitid%.2DA~  ~override~
  REPLACE_TEXTUALLY ~AP_RR#BJSIT~  ~****~
  REPLACE_TEXTUALLY ~AP_d2BIjst~  ~****~
  REPLACE_TEXTUALLY ~GA_C0JES01~  ~****~
  REPLACE_TEXTUALLY ~AP_%m1%~  ~****~
  REPLACE_TEXTUALLY ~AP_%m5%~  ~****~
  PRETTY_PRINT_2DA
BUT_ONLY


//rr - add jester passives
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~RR#BJSIT~) BEGIN
  COPY_EXISTING ~%kitid%.2DA~  ~override~
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~RR#BJSIT~ END
  BUT_ONLY
END


//Shohy bard mod
ACTION_IF (RESOURCE_CONTAINS ~CLABBA02.2da~ ~BI#B20A~) BEGIN

  // edit jester passives
  COPY_EXISTING ~BI#B30A.spl~  ~override/d2BIjst.SPL~
    LPF DELETE_EFFECT INT_VAR match_opcode=251 END
    LPF DELETE_EFFECT INT_VAR match_opcode=146 STR_VAR match_resource=~BI#B00D~ END    // remove choir abilities
  IF_EXISTS

  // add jester passives (already has blade stuff)
  COPY_EXISTING ~%kitid%.2DA~  ~override~
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~d2BIjst~ END
  BUT_ONLY

/*moved to hlaupdatermk1.tph
  // change HLA to jester song
  COPY_EXISTING ~%hlatable%.2DA~  ~override~
    REPLACE_TEXTUALLY EXACT_MATCH ~AP_BI#B2SS~  ~AP_BI#B3SS~
  BUT_ONLY
*/

  // give "chorus" abilities (with hla song)
  COPY_EXISTING ~BI#B3SS.spl~  ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode=326 match_parameter1=IDS_OF_SYMBOL (kit ~%ids_name%~) END
    LPF CLONE_EFFECT
      INT_VAR match_opcode=251 opcode=326 parameter2=109 parameter1=IDS_OF_SYMBOL (kit ~%ids_name%~)
      STR_VAR resource=~BI#B00D~
    END
  IF_EXISTS BUT_ONLY

  // hide 2 weapon prof (given automatically), shohy doesn't currently hide for blades
  LAF d2_weapprof_styles INT_VAR d=0 STR_VAR kit=d2jesterblade END

END


//
//Bardic Wonders - add Heckle ability
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~C0JES01~) BEGIN
  COPY_EXISTING ~%kitid%.2DA~  ~override~
    LPF d2_kit_ability_adder INT_VAR multi=1 increment=1 STR_VAR resource=~C0JES01~ END
  BUT_ONLY
END


//bardic wonders - song tweaks
OUTER_TEXT_SPRINT bardic1  ~%s1%~
OUTER_TEXT_SPRINT bardic2  ~%s1%s~

//visual tweak
ACTION_IF ((FILE_EXISTS ~override/c0bsngef.vvc~) AND
           (FILE_EXISTS ~override/c0bsngef.bam~)
) BEGIN
  COPY ~override/%bardic1%.spl~  ~override~
    LPF CLONE_EFFECT
      INT_VAR match_opcode=215 target=1 parameter2=1 timing=10 duration=7 savingthrow=0 savebonus=0
      STR_VAR resource=c0bsngef insert=first
    END
  BUT_ONLY
END

//song mechanics
ACTION_IF ((FILE_EXISTS ~override/c0bardsx.spl~) AND
           (FILE_EXISTS ~override/c0singi2.spl~) AND
           (FILE_EXISTS ~override/SPCL751A.eff~) AND
           (FILE_EXISTS ~override/SPCL751A.spl~)
) BEGIN

  // adjust file structure
  COPY ~override/%bardic1%.spl~  ~override/%bardic2%.SPL~
    LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%SOURCE_RES%~ resource=~%DEST_RES%~ END
  COPY ~override/SPCL751A.spl~    ~override/%bardic1%.SPL~
    WRITE_LONG 0x8 ~-1~
    LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%SOURCE_RES%~ resource=~%DEST_RES%~ END
  COPY ~override/SPCL751A.eff~    ~override/%bardic1%.eff~
    WRITE_ASCII 0x30 ~%bardic2%~ #8

  // update kit desc
  LAF GET_KIT_STRREF STR_VAR kit_name=~%ids_name%~ RET kit_strref END
  ACTION_GET_STRREF EVAL ~%kit_strref%~ desc
  OUTER_SPRINT bardic_tweak @650

  ACTION_IF (~%desc%~ STRING_CONTAINS_REGEXP ~%bardic_tweak%~) BEGIN    // if "desc" does NOT contain "bardic_tweak"
    OUTER_TEXT_SPRINT kittext ~%desc%%WNL%%WNL%%bardic_tweak%~
    STRING_SET_EVALUATE kit_strref  ~%kittext%~
  END

END


/*
The OUTER_SPRINT stuff below was just experimenting (combines text from IWDification and Bardic Wonders tweaks).
NOTE: Bardic Wonders Jester/Skald revisions are not fully compatible with IWDification (you can make it work, but there will be weirdness). The tweaks section should be fine.
*/

OUTER_SPRINT songchange @205
ACTION_IF (VARIABLE_IS_SET bardic_tweak) BEGIN
  OUTER_SPRINT desc @205
  OUTER_SPRINT note @650
  OUTER_SPRINT songchange ~%desc%%WNL%%WNL%%note%~
END


//
//IWDification (jester/skald song tweak)
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~cdcl751~) BEGIN
  COPY ~override/cdcl751.spl~  ~override/%m1%.SPL~
    SAY NAME1 @105
    SAY UNIDENTIFIED_DESC ~%songchange%~
    LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s1%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=171 STR_VAR resource=~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%DEST_RES%~ END

    // tweak casting
    LPF ALTER_SPELL_HEADER INT_VAR target=7 END
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END
    LPF CLONE_EFFECT INT_VAR match_opcode=251 opcode=174 timing=1 STR_VAR resource=EFF_M02 END
  IF_EXISTS

  // add to kit clab
  ACTION_IF (FILE_EXISTS ~override/%m1%.SPL~) BEGIN
    COPY_EXISTING ~%kitid%.2DA~  ~override~
      LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%m1%~ END
    BUT_ONLY
  END
END


//ToF selectable Jester song (IWD songs + minor/HLAs)
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~dw-sng2~) BEGIN
  COPY ~override/dw-sng2.spl~  ~override/%m1%.SPL~
    SAY NAME1 @105
    SAY UNIDENTIFIED_DESC ~%songchange%~
    LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s1%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=171 STR_VAR resource=~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%DEST_RES%~ END

    // tweak casting
    LPF ALTER_SPELL_HEADER INT_VAR target=7 END
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END
    LPF CLONE_EFFECT INT_VAR match_opcode=251 opcode=174 timing=1 STR_VAR resource=EFF_M02 END
  IF_EXISTS

  // add to kit clab
  ACTION_IF (FILE_EXISTS ~override/%m1%.SPL~) BEGIN
    COPY_EXISTING ~%kitid%.2DA~  ~override~
      LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%m1%~ END
    BUT_ONLY
  END
END


/*changed to make ambidexterity choosable as feat
//give ToF ambidexterity at level 20
ACTION_IF (RESOURCE_CONTAINS ~%hlatable%.2da~  ~dw-ftamb~) BEGIN
  COPY ~%abilities%/applyeffects.spl~  ~override/%m5%.spl~
    LPF ALTER_EFFECT STR_VAR resource=~dw-ftamb~ END
    LPF CLONE_EFFECT INT_VAR opcode=313 timing=9 END

  COPY_EXISTING ~%kitid%.2DA~  ~override~
    LPF d2_kit_ability_adder INT_VAR level=20 passive=1 STR_VAR resource=~%m5%~ END
  BUT_ONLY
END
*/


//
//dummy files (so updater doesn't create new files)
COPY_EXISTING - ~SPCL142.SPL~  ~inlined/dummy.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=0 parameter1=0 parameter2=0 timing=0 duration=0 END

ACTION_FOR_EACH spell IN
  ~d2BIjst~
  ~%bardic2%~
  ~%m1%~
//  ~%m5%~
BEGIN
  ACTION_IF !(FILE_EXISTS ~override/%spell%.SPL~) BEGIN
    COPY ~inlined/dummy.SPL~  ~override/%spell%.SPL~
  END
END

ACTION_IF !(FILE_EXISTS ~override/%bardic1%.eff~) BEGIN
  COPY ~%abilities%/castspellself.EFF~  ~override/%bardic1%.EFF~
END


//variables
OUTER_TEXT_SPRINT heckle  ~C0JES01~


//
//